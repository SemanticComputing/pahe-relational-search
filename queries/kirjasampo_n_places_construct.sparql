PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX nbf:	<http://ldf.fi/nbf/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX relse: <http://ldf.fi/relse/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skosxl: <http://www.w3.org/2008/05/skos-xl#>
BASE <http://ldf.fi/relse/>
PREFIX re: <http://www.w3.org/2000/10/swap/reason#>
PREFIX rel: <http://purl.org/vocab/relationship/>

CONSTRUCT {
  ?uri a relse:Relation ;
    relse:personSubject ?person ;
    relse:placeObject ?place ;
    skos:prefLabel ?description ;
    relse:relationType relse:novelDepictsPlace ;
    relse:source ?source ;
    relse:date ?date ;
    relse:sourceName "Kirjan tiedot Kirjasammossa"@fi ;
    relse:sourceLink ?sampoLink ;
    
}
WHERE {
  ?relation a relse:Relation .
  ?relation relse:source ?source .
  ?relation relse:personSubject ?subject .
  ?relation relse:placeObject ?object .
  
  ?person nbf:kirjasampo ?subject .  # Not general
  
  ?place a relse:Place .

  ?place skos:prefLabel ?placeName .
  ?relation relse:placeName ?placeName .
  FILTER(str(?placeName) != "Suomi") .
  
  ?relation relse:date ?dateRaw .
  ?relation relse:objectName ?bookName .
  BIND (xsd:date(?dateRaw) as ?date ) .
  
  ?person skosxl:prefLabel ?personLabel .
  ?personLabel schema:familyName ?familyName .
  ?personLabel schema:givenName ?givenName .
  
  BIND(replace(str(?source), "http://seco.tkk.fi/", "https://www.kirjasampo.fi/fi/kulsa/") as ?saha_replaced) .
  BIND(replace(str(?saha_replaced), "http://www.btj.fi/at_", "https://www.kirjasampo.fi/fi/kulsa/http%253A%252F%252Fwww.btj.fi%252Fft_") as ?btj_replaced) .
  BIND(uri(replace(str(?btj_replaced), "http://www.yso.fi/onto/kaunokki#", "https://www.kirjasampo.fi/fi/kulsa/kauno%253A")) as ?sampoLink) .
  
  BIND(uri(encode_for_uri(concat(str(?person), str(?place), str(?source), "novel_depicts"))) as ?uri) .
  BIND(concat(str(?givenName), " ", str(?familyName), " on kirjoittanut romaanin '", str(?bookName), "' joka kuvaa paikkaa ", str(?placeName), ".") as ?description) .
}
