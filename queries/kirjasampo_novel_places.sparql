PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX kaunokki: <http://www.yso.fi/onto/kaunokki#>
PREFIX saha: <http://seco.tkk.fi/saha3/>
PREFIX btj: <http://www.btj.fi/>
PREFIX relse: <http://ldf.fi/relsearch/>

CONSTRUCT {
  [ a relse:Relation ;
      relse:date ?year ;
      relse:relationType relse:novelDepictsPlace ;
      relse:personSubject ?writer ;
      relse:placeObject ?place ;
      relse:source ?work ;
      relse:placeName ?placeName ;
      relse:objectName ?workName ]
}
WHERE {
  ?work a <http://www.yso.fi/onto/kaunokki#romaani> .
  ?work <http://www.yso.fi/onto/kaunokki#worldPlace> ?place .
  ?work <http://www.yso.fi/onto/kaunokki#tekija> ?writer .
  ?work skos:prefLabel ?workName .
  ?work <http://www.yso.fi/onto/kaunokki#manifests_in> ?version .
  ?version <http://www.yso.fi/onto/kaunokki#onEnsimmainenVersio> <http://www.yso.fi/onto/kaunokki#true> .
  ?version <http://www.yso.fi/onto/kaunokki#ilmestymisvuosi> ?date .
  ?date <http://www.yso.fi/onto/time-schema#earliestStart> ?dateStart .
  ?date skos:prefLabel ?dateLabel .
  BIND(str(?dateLabel) AS ?year) .
  ?writer skos:prefLabel ?writerName .
  ?place skos:prefLabel ?placeName .
  FILTER(lang(?placeName) = 'fi') .
  FILTER(lang(?workName) = 'fi') .
}
