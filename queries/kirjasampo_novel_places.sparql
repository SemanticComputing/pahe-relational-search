PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX kaunokki: <http://www.yso.fi/onto/kaunokki#>
PREFIX saha: <http://seco.tkk.fi/saha3/>
PREFIX btj: <http://www.btj.fi/>
PREFIX relse: <http://ldf.fi/relsearch/>
PREFIX yso: <http://www.yso.fi/onto/yso/>

CONSTRUCT {
  [ a relse:Relation ;
      relse:date ?date ;
      relse:relationType relse:novelDepictsPlace ;
      relse:personSubject ?writer ;
      relse:placeObject ?place ;
      relse:source ?work ;
      relse:placeName ?placeName ;
      relse:objectName ?workName ]
}
WHERE {
  VALUES ?novel_class { <http://www.yso.fi/onto/kaunokki#romaani> <http://www.yso.fi/onto/kito/k351> }
  ?work a ?novel_class .
  ?work <http://www.yso.fi/onto/kaunokki#worldPlace> ?place . # Not general
  ?work <http://www.yso.fi/onto/kaunokki#tekija> ?writer . # Not general
  ?work skos:prefLabel|rdfs:label ?workName .
  
    # Complicated way to get date that is unlikely to be general
  OPTIONAL {
  	?work <http://www.yso.fi/onto/kaunokki#manifests_in> ?version .
  	?version <http://www.yso.fi/onto/kaunokki#onEnsimmainenVersio> <http://www.yso.fi/onto/kaunokki#true> .
  	?version <http://www.yso.fi/onto/kaunokki#ilmestymisvuosi> ?pubYear .
  	?pubYear <http://www.yso.fi/onto/time-schema#earliestStart> ?startDate .
    	BIND(xsd:dateTime(?startDate) as ?date) .   #gives date time, maybe should be date?
  }
  
  ?writer skos:prefLabel|rdfs:label ?writerName .
  ?place skos:prefLabel|rdfs:label ?placeName .
  FILTER(lang(?placeName) = 'fi') .
  FILTER(lang(?workName) = 'fi') .
}
