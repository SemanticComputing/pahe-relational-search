PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX schema: <http://schema.org/>
PREFIX nbf:	<http://ldf.fi/nbf/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX gvp: <http://vocab.getty.edu/ontology#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rel: <http://ldf.fi/relse/>
BASE <http://ldf.fi/relse/>
PREFIX skosxl: <http://www.w3.org/2008/05/skos-xl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
    ?uri a rel:Relation ;
        rel:relationType rel:deathPlace ;
        rel:personSubject ?person ;
        rel:placeObject ?place ;
	rel:source ?death ;
        rel:date ?deathtime ;
	skos:prefLabel ?description ;
	rel:sourceName "Semanttinen Kansallisbiografia"@fi ;
	rel:sourceLink ?death ;
}

WHERE {
    VALUES ?place_class { schema:Place nbf:Place rel:Place }
    VALUES ?person_class { gvp:PersonConcept foaf:Person }
    ?person a|(a/rdfs:subClassOf+) ?person_class .
    ?death crm:P100_was_death_of/^foaf:focus ?person .
    ?death ?any ?place . # nbf:place!
    ?place a|(a/rdfs:subClassOf+) ?place_class . 

    ?death nbf:time ?dtime . # Not general!
    ?dtime gvp:estStart ?deathtime .
    BIND (year(xsd:date(?deathtime)) As ?year)

    ?place skos:prefLabel ?placeName .

    ?person skosxl:prefLabel ?personLabel .
    ?personLabel skos:prefLabel ?personName .
    ?personLabel schema:familyName ?familyName .
    ?personLabel schema:givenName ?givenName .

    BIND(uri(encode_for_uri(concat(str(?person), str(?place), "death_place", str(?death)))) as ?uri) .

    BIND(concat(str(?givenName), " ", str(?familyName), " on kuollut paikkassa ", str(?placeName), " vuonna ", str(?year), ".") as ?description) .

}
