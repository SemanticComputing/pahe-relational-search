PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX relse: <http://ldf.fi/relse/>
PREFIX snellman: <http://ldf.fi/snellman/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
    
CONSTRUCT {
  [ a relse:Relation ;
    relse:personSubject ?correspondent ;
    relse:placeObject ?place ;
    relse:date ?date ;
    relse:relationType relse:letterReceivedFrom ;
    relse:source ?document ;
    relse:sourceLink ?source ;
    relse:sourceName "Kirje J. V. Snellmanin kirjeenvaihdossa" ;
  ]
}
WHERE
{
  ?document a snellman:Document .
  ?document snellman:relatedCorrespondence ?correspondence .
  ?correspondence snellman:correspondent ?correspondent .
  ?document dc:creator ?writer .
  FILTER (?writer != ?correspondent) .
  ?document snellman:writtenIn ?place .
  ?document dc:date ?date .
  ?document dc:source ?source . 
}
